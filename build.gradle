plugins {
  id 'java' //java support
  id 'groovy' // groovy support
  id 'maven-publish'
  id 'signing'
  id 'pmd' // code check, working on source code
  id 'com.diffplug.spotless' version '7.0.2' //code format
  id 'com.github.spotbugs' version '6.1.7' // code check, working on byte code
  id 'de.undercouch.download' version '5.6.0'
  id 'kr.motd.sphinx' version '2.10.1' // documentation generation
  id 'jacoco' // java code coverage plugin
  id "org.sonarqube" version "6.1.0.5360" // sonarqube
  id 'net.thauvin.erik.gradle.semver' version '1.0.4' // semantic versioning
  id "com.github.johnrengelman.shadow" version "8.1.1" // fat jar
}

ext {
  //version (changing these should be considered thoroughly!)
  javaVersion = JavaVersion.VERSION_17
  groovyVersion = "4.0"
  groovyBinaryVersion = "4.0.26"

  junitVersion = '1.12.0'
  testcontainersVersion = '1.21.0'

  scriptsLocation = 'gradle' + File.separator + 'scripts' + File.separator //location of script plugins
}

group = 'com.github.ie3-institute'
description = 'PowerSystemDataModel'

java {
  sourceCompatibility = javaVersion
  targetCompatibility = javaVersion
}

apply from: scriptsLocation + 'tests.gradle'
apply from: scriptsLocation + 'pmd.gradle'
apply from: scriptsLocation + 'spotbugs.gradle'
apply from: scriptsLocation + 'spotless.gradle'
apply from: scriptsLocation + 'checkJavaVersion.gradle'
apply from: scriptsLocation + 'documentation.gradle'
apply from: scriptsLocation + 'jacoco.gradle' // jacoco java code coverage
apply from: scriptsLocation + 'mavenCentralPublish.gradle'
apply from: scriptsLocation + 'sonarqube.gradle'
apply from: scriptsLocation + 'vcs.gradle'
apply from: scriptsLocation + 'semVer.gradle'
apply from: scriptsLocation + 'branchName.gradle' // checks naming scheme of branches

repositories {
  mavenCentral() // searches in Sonatype's repository 'Maven Central'

  // sonatype snapshot repo
  maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {

  //	 ieÂ³ power system utils
  implementation 'com.github.ie3-institute:PowerSystemUtils:3.0.0'

  implementation 'tech.units:indriya:2.2.2'

  // JTS Topology Suite for GeoPositions, License: EPL 1.0 / EDL 1.0
  implementation ('org.locationtech.jts:jts-core:1.20.0'){
    exclude group: 'junit', module: 'junit'
  }

  implementation 'org.locationtech.jts.io:jts-io-common:1.20.0'

  // Graphs
  implementation 'org.jgrapht:jgrapht-core:1.5.2'

  // Statistics (for random load model)
  implementation 'de.lmu.ifi.dbs.elki:elki:0.7.5'

  // testing
  testImplementation "org.apache.groovy:groovy:$groovyBinaryVersion"

  testImplementation "org.junit.platform:junit-platform-launcher:$junitVersion"
  testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
  testImplementation "org.spockframework:spock-core:2.3-groovy-$groovyVersion"
  testImplementation 'org.objenesis:objenesis:3.4' // Mock creation with constructor parameters
  testImplementation 'net.bytebuddy:byte-buddy:1.17.5' // Mocks of classes

  // testcontainers (docker framework for testing)
  testImplementation "org.testcontainers:testcontainers:$testcontainersVersion"
  testImplementation "org.testcontainers:spock:$testcontainersVersion"
  testImplementation "org.testcontainers:influxdb:$testcontainersVersion"
  testImplementation "org.testcontainers:postgresql:$testcontainersVersion"
  testImplementation "org.testcontainers:couchbase:$testcontainersVersion"

  // logging
  implementation platform('org.apache.logging.log4j:log4j-bom:2.24.3')
  implementation 'org.apache.logging.log4j:log4j-api' // log4j
  implementation 'org.apache.logging.log4j:log4j-core' // log4j
  implementation 'org.apache.logging.log4j:log4j-slf4j-impl' // log4j -> slf4j

  // Databases
  implementation 'org.influxdb:influxdb-java:2.25'
  implementation 'com.couchbase.client:java-client:3.8.0'
  runtimeOnly 'org.postgresql:postgresql:42.7.5' // postgresql jdbc driver required during runtime

  implementation 'commons-io:commons-io:2.19.0' // I/O functionalities
  implementation 'commons-codec:commons-codec:1.18.0' // needed by commons-compress
  implementation 'org.apache.commons:commons-compress:1.27.1' // I/O functionalities
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

tasks.withType(Javadoc){
  options.encoding = 'UTF-8'
  failOnError = false // TODO: Temp until JavaDoc issues are resolved
}

tasks.register('printVersion') {
  doLast {
    println project.version
  }
}
